El siguiente código es una implementación de un intérprete del cálculo lambda, incluye un analizador sintáctico, un evaluador y procedimientos específicos para generar código de \LaTeX~a partir de la representación interna del intérprete.

La implementación está escrita en \texttt{Racket} y se divide en cuatro módulos.


\section{Módulo principal}
\label{ap:2:principal}

\begin{lstlisting}[language=racket]
#lang racket/base

(require racket/match)

(provide
 atom          atom?         atom-symbol
 application   application?  application-applicator  application-applicand
 abstraction   abstraction?  abstraction-argument    abstraction-body
 command       command?      command-name            command-arguments
 assert!
 *terminal-delimiters* *initial-delimiters*
 terminal-delimiter?   initial-delimiter?    delimiter?
 consume-whitespace
 read-number read-identifier read-brackets read-command/atom
 read-lambda read-parentheses read-expr
 apply/identity
 *metacommands* *commands*
 eval-expr
 notation-abuse?
 *command-formats*
 fmt:atom-pre fmt:atom-post
 fmt:command-name-pre fmt:command-name-post
 fmt:other-pre fmt:other-post
 fmt:left-paren fmt:right-paren fmt:left-brack fmt:right-brack
 fmt:application-sep fmt:abstraction-sep fmt:command-sep
 fmt:lambda fmt:dot
 format-atom format-application format-abstraction format-command format-expr
 format-list format-other
 print-expr)

(struct atom (symbol) #:transparent)
(struct application (applicator applicand) #:transparent)
(struct abstraction (argument body) #:transparent)
(struct command (name arguments) #:transparent)

(define (assert! ok? who message . formats)
  (if ok? #t (apply error who message formats)))

(define *terminal-delimiters* '(#\} #\] #\) #\. #\, ))

(define *initial-delimiters* '(#\{ #\[ #\( #\lambda #\\ ))

(define (terminal-delimiter? ch)
  (or (eof-object? ch) (member ch *terminal-delimiters* char=?)))

(define (initial-delimiter? ch)
  (and (not (eof-object? ch)) (member ch *initial-delimiters* char=?)))

(define (delimiter? ch)
  (or (terminal-delimiter? ch)
      (initial-delimiter? ch)
      (char-whitespace? ch)))

(define (consume-whitespace [in (current-input-port)]
                            [ch (peek-char in)])
  (when (and (char? ch) (char-whitespace? ch))
    (read-char in)
    (consume-whitespace in (peek-char in))))

(define (read-number [in (current-input-port)]
                     [ch (peek-char in)])
  (consume-whitespace in ch)
  (let loop ([lis null]
             [n   (peek-char in)])
    (cond [(and (char? n) (char-numeric? n))
           (read-char in)
           (loop (cons n lis) (peek-char in))]
          [(null? lis)
           (error 'read-number "expecting digit but got `~a'" n)]
          [else
           (assert! (delimiter? n) 'read-number
                    "missing delimiter, got `~a'" n)
           (string->number (list->string (reverse lis)))])))

(define (read-identifier [in (current-input-port)]
                         [ch (peek-char in)])
  (consume-whitespace in ch)
  (let loop ([lis null]
             [x   (peek-char in)])
    (cond [(not (delimiter? x))
           (read-char in)
           (loop (cons x lis) (peek-char in))]
          [(null? lis)
           (error 'read-identifier "expecting a non-delimiter but got `~a'" x)]
          [else
           (assert! (delimiter? x) 'read-identifier
                    "missing delimiter, got `~a'" x)
           (string->symbol (list->string (reverse lis)))])))

(define (read-brackets [in (current-input-port)]
                       [ch (peek-char in)])
  (consume-whitespace in ch)
  (let ([ch (read-char in)])
    (assert! (and (char? ch) (char=? ch #\[)) 'read-brackets
             "expecting `[' but got `~a'" ch))
  (let loop ([lis null]
             [arg (read-expr in)])
    (cond [(null? arg)
           (let ([ch (read-char in)])
             (cond [(eof-object? ch)
                    (error 'read-brackets "missing matching bracket")]
                   [(char=? ch #\,)
                    (loop lis (read-expr in))]
                   [(char=? ch #\])
                    (reverse lis)]
                   [else
                    (error 'read-brackets "missing matching bracket")]))]
          [else
            (loop (cons arg lis) (read-expr in))])))

(define (read-command/atom [in (current-input-port)]
                           [ch (peek-char in)])
  (define id (read-identifier in ch))
  (consume-whitespace in)
  (let ([ch (peek-char in)])
    (cond [(and (char? ch) (char=? ch #\[))
           (command id (read-brackets in ch))]
          [else
           (atom id)])))

(define (read-lambda [in (current-input-port)]
                     [ch (peek-char in)])
  (consume-whitespace in ch)
  (let ([ch (read-char in)])
    (assert! (and (char? ch) (or (char=? ch #\\ ) (char=? ch #\lambda))) 'read-lambda
             "expecting `lambda' or `\\' but got `~a'" ch))
  (let loop ([lis (list (read-command/atom in))]
             [ch  (peek-char in)])
    (cond [(char=? ch #\.)
           (read-char in)
           (foldr abstraction (read-expr in) (reverse lis))]
          [else
           (loop (cons (read-command/atom in) lis) (peek-char in))])))

(define (apply/identity acc x)
  (if (null? acc) x (application acc x)))

(define (read-parentheses [in (current-input-port)]
                          [ch (peek-char in)])
  (consume-whitespace in ch)
  (let ([ch (read-char in)])
    (assert! (and (char? ch) (char=? ch #\( )) 'read-parentheses
             "expecting `(' but got `~a'" ch))
  (let ([expr (read-expr in)])
    (assert! (or (application? expr) (abstraction? expr)) 'read-parentheses
             "expected an application or an abstraction but got ~a" expr)
    (let ([ch (read-char in)])
      (assert! (and (char? ch) (char=? ch #\) )) 'read-parentheses
               "missing matching parentheses"))
    expr))

(define (read-expr [in  (current-input-port)]
                   [ch  (peek-char in)]
                   [acc null])
  (cond [(or (eof-object? ch) (terminal-delimiter? ch))
         acc]
        [(char-whitespace? ch)
         (consume-whitespace in ch)
         (read-expr in (peek-char in) acc)]
        [(char-numeric? ch)
         (read-number in ch)]
        [(char=? ch #\( )
         (let ([expr (read-parentheses in ch)])
           (read-expr in (peek-char in) (apply/identity acc expr)))]
        [(or (char=? ch #\lambda) (char=? ch #\\ ))
         (let ([expr (read-lambda in ch)])
           (read-expr in (peek-char in) (apply/identity acc expr)))]
        [else
         (let ([expr (read-command/atom in ch)])
           (read-expr in (peek-char in) (apply/identity acc expr)))]))

(define *metacommands* (make-hasheq))
(define *commands* (make-hasheq))

(define (eval-expr expr)
  (cond [(atom? expr)
         expr]
        [(application? expr)
         (application (eval-expr (application-applicator expr))
                      (eval-expr (application-applicand expr)))]
        [(abstraction? expr)
         (abstraction (eval-expr (abstraction-argument expr))
                      (eval-expr (abstraction-body expr)))]
        [(command? expr)
         (cond [(hash-ref *metacommands* (command-name expr) #f)
                => (lambda (proc) (apply proc (command-arguments expr)))]
               [(hash-ref *commands* (command-name expr) #f)
                => (lambda (proc) (apply proc (map eval-expr (command-arguments expr))))]
               [else
                (error 'eval-expr "unknown command ~a called with arguments ~a"
                       (command-name expr) (command-arguments expr))])]
        [else
         expr]))

(define fmt:atom-pre (make-parameter ""))
(define fmt:atom-post (make-parameter ""))
(define fmt:application-sep (make-parameter " "))
(define fmt:lambda (make-parameter "lambda"))
(define fmt:dot (make-parameter "."))
(define fmt:abstraction-sep (make-parameter " "))
(define fmt:command-name-pre (make-parameter ""))
(define fmt:command-name-post (make-parameter ""))
(define fmt:command-sep (make-parameter ","))
(define fmt:other-pre (make-parameter ""))
(define fmt:other-post (make-parameter ""))
(define fmt:left-paren (make-parameter "("))
(define fmt:right-paren (make-parameter ")"))
(define fmt:left-brack (make-parameter "["))
(define fmt:right-brack (make-parameter "]"))
(define notation-abuse? (make-parameter #f))

(define *command-formats* (make-hasheq))

(define (format-atom symbol)
 (format "~a~a~a" (fmt:atom-pre) symbol (fmt:atom-post)))

(define (format-application applicator applicand)
  (cond
    [(notation-abuse?)
     (format "~a~a~a"
             (if (abstraction? applicator)
                 (format "~a~a~a"
                         (fmt:left-paren)
                         (format-expr applicator)
                         (fmt:right-paren))
                 (format-expr applicator))
             (fmt:application-sep)
             (if (or (application? applicand)
                     (abstraction? applicand))
                 (format "~a~a~a"
                         (fmt:left-paren)
                         (format-expr applicand)
                         (fmt:right-paren))
                 (format-expr applicand)))]
    [else
     (format "~a~a~a~a~a"
             (fmt:left-paren) (format-expr applicator)
             (fmt:application-sep)
             (format-expr applicand) (fmt:right-paren))]))

(define (format-list lis sep)
  (cond [(null? lis) ""]
        [(null? (cdr lis))
         (format-expr (car lis))]
        [else
         (format "~a~a~a"
                 (format-expr (car lis))
                 sep
                 (format-list (cdr lis) sep))]))

(define (format-abstraction argument body)
  (cond [(notation-abuse?)
         (match body
           [(abstraction x M)
            (format-abstraction
             (if (list? argument) (cons x argument) (list x argument)) M)]
           [_
            (format "~a~a~a~a"
                    (fmt:lambda)
                    (if (list? argument)
                        (format-list (reverse argument) (fmt:abstraction-sep))
                        (format-expr argument))
                    (fmt:dot)
                    (format-expr body))])]
        [else
         (format "~a~a~a~a~a~a"
                 (fmt:left-paren) (fmt:lambda)
                 (format-expr argument)
                 (fmt:dot)
                 (format-expr body)
                 (fmt:right-paren))]))

(define (format-command name arguments)
  (cond [(hash-ref *command-formats* name #f)
         => (lambda (proc)
              (apply proc arguments))]
        [else
         (format "~a~a~a~a~a~a"
                 (fmt:command-name-pre) name (fmt:command-name-post)
                 (fmt:left-brack)
                 (format-list arguments (fmt:command-sep))
                 (fmt:right-brack))]))

(define (format-other other)
  (format "~a~a~a"
          (fmt:other-pre)
          other
          (fmt:other-post)))

(define (format-expr expr)
  (match expr
    [(atom symbol)                      (format-atom symbol)]
    [(application applicator applicand) (format-application applicator applicand)]
    [(abstraction argument body)        (format-abstraction argument body)]
    [(command name arguments)           (format-command name arguments)]
    [x                                  (format-other x)]))

(define (print-expr expr [out (current-output-port)])
  (display (format-expr expr) out))

\end{lstlisting}

\section{Módulo comandos}
\label{ap:2:comandos}

\begin{lstlisting}[language=racket]
#lang racket/base

(require racket/match)
(require racket/list)
(require racket/set)

(require "lambda-common.rkt")

(provide
 install-procedure
 cmd:abuse
 cmd:unabuse
 fmt:fv-name
 fmt:length-pre fmt:length-post
 fmt:subst-assign)

(define (install-procedure table name procedure)
  (hash-set! table name procedure))

(define (cmd:quote expr)
  (match expr
    [(atom symbol)
     expr]
    [(application applicator applicand)
     (application (cmd:quote applicator)
                  (cmd:quote applicand))]
    [(abstraction argument body)
     (abstraction (cmd:quote argument)
                  (cmd:quote body))]
    [(command name arguments)
     (if (eq? name 'u)
         (cmd:unquote (car arguments))
         (command name
                  (map cmd:quote arguments)))]
    [_
     expr]))

(install-procedure *metacommands* 'q cmd:quote)

(define (cmd:unquote expr)
  (eval-expr expr))

(install-procedure *metacommands* 'u cmd:unquote)

(define (cmd:numeral expr)
  (match expr
    [(abstraction (atom symbol1) (abstraction (atom symbol2) body))
     (let loop ([body body])
       (match body
         [(== (atom symbol2))
          0]
         [(application (== (atom symbol1)) body)
          (+ 1 (loop body))]
         [_
          (error 'numeral "malformed Church encoding")]))]
    [_
     (error 'numeral "malformed Church encoding")]))

(install-procedure *commands* 'numeral cmd:numeral)

(define (cmd:length expr)
  (match expr
    [(atom symbol) 1]
    [(application applicator applicand)
     (+ (cmd:length applicator)
        (cmd:length applicand))]
    [(abstraction argument body)
     (+ 1 (cmd:length body))]
    [_
     (error 'length "can't compute the length of ~a, it's not a lambda term" expr)]))

(install-procedure *commands* 'length cmd:length)

(define fmt:length-pre (make-parameter "|"))
(define fmt:length-post (make-parameter "|"))

(define (fmt:length expr)
 (format "~a~a~a"
          (fmt:length-pre)
          (format-expr expr)
          (fmt:length-post)))

(install-procedure *command-formats* 'length fmt:length)

(define (cmd:free-variables expr)
  (match expr
    [(atom symbol)
     (list expr)]
    [(application applicator applicand)
     (remove-duplicates (append (cmd:free-variables applicator)
                                (cmd:free-variables applicand)))]
    [(abstraction argument body)
     (remove argument (cmd:free-variables body))]
    [_
     (error 'length "can't compute the length of ~a, it's not a lambda term" expr)]))

(install-procedure *commands* 'fv cmd:free-variables)

(define fmt:fv-name (make-parameter "FV"))

(define (fmt:free-variables expr)
  (format "~a~a~a~a"
          (fmt:fv-name)
          (fmt:left-paren)
          (format-expr expr)
          (fmt:right-paren)))

(install-procedure *command-formats* 'fv fmt:free-variables)

(define alphabetic-strings
  (map (lambda (n) (string (integer->char n)))
       (range (char->integer #\a) (char->integer #\z))))

(define (atom-from-new-symbol binding-symbols)
  (let ([diff (set-subtract (map string->symbol alphabetic-strings)
                            binding-symbols)])
    (if (not (null? diff))
        (atom (car diff))
        (let loop ([i 0])
          (let ([next-symbols (map (lambda (str)
                                     (string->symbol (string-append str (number->string i))))
                                   alphabetic-strings)])
            (let ([diff (set-subtract next-symbols binding-symbols)])
              (if (not (null? diff))
                  (atom (car diff))
                  (loop (+ i 1)))))))))

(define (subst expr old-atom new-expr binding-symbols)
  (match expr
    [(atom symbol)
     (if (equal? expr old-atom)
         new-expr
         expr)]
    [(application applicator applicand)
     (application (subst applicator old-atom new-expr binding-symbols)
                  (subst applicand old-atom new-expr binding-symbols))]
    [(abstraction argument body)
     (cond
       [(or (equal? argument old-atom)
            (not (member old-atom (cmd:free-variables body))))
        expr]
       [(not (member argument (cmd:free-variables new-expr)))
        (abstraction argument
                     (subst body old-atom new-expr
                            (set-add binding-symbols (atom-symbol argument))))]
       [else
        (let* ([binding-symbols (set-union binding-symbols
                                           (map atom-symbol (cmd:free-variables new-expr))
                                           (map atom-symbol (cmd:free-variables body)))]
               [fresh (atom-from-new-symbol binding-symbols)])
          (abstraction fresh
                       (subst (subst body argument fresh
                                     (set-add binding-symbols (atom-symbol fresh)))
                              old-atom new-expr
                              (set-add binding-symbols (atom-symbol fresh)))))])]))

(define (cmd:subst expr old-atom new-expr)
  (subst expr old-atom new-expr null))

(install-procedure *commands* 'subst cmd:subst)

(define fmt:subst-assign (make-parameter ":="))

(define (fmt:subst expr old-atom new-expr)
  (if (and (notation-abuse?)
           (or (application? expr) (abstraction? expr)))
      (format "~a~a~a~a~a~a~a~a"
              (fmt:left-paren)
              (format-expr expr)
              (fmt:right-paren)
              (fmt:left-brack)
              (format-expr old-atom)
              (fmt:subst-assign)
              (format-expr new-expr)
              (fmt:right-brack))
      (format "~a~a~a~a~a~a"
              (format-expr expr)
              (fmt:left-brack)
              (format-expr old-atom)
              (fmt:subst-assign)
              (format-expr new-expr)
              (fmt:right-brack))))

(install-procedure *command-formats* 'subst fmt:subst)

(define (cmd:abuse)
  (notation-abuse? #t))

(install-procedure *metacommands* 'abuse cmd:abuse)

(define (cmd:unabuse)
  (notation-abuse? #f))

(install-procedure *metacommands* 'unabuse cmd:unabuse)

(define (fmt:well-formed expr)
  (let ([state (notation-abuse?)])
    (cmd:unabuse)
    (let ([str (format-expr expr)])
      (notation-abuse? state)
      str)))

(install-procedure *command-formats* 'wf fmt:well-formed)

(define (cmd:well-formed expr)
  (cmd:quote (command 'wf (list expr))))

(install-procedure *commands* 'wf cmd:well-formed)

(define (fmt:mal-formed expr)
  (let ([state (notation-abuse?)])
    (cmd:abuse)
    (let ([str (format-expr expr)])
      (notation-abuse? state)
      str)))

(install-procedure *command-formats* 'mf fmt:mal-formed)

(define (cmd:mal-formed expr)
  (cmd:quote (command 'mf (list expr))))

(install-procedure *commands* 'mf cmd:mal-formed)

(define (fmt:hole)
  (format "~a \\quad ~a"
          (fmt:left-brack)
          (fmt:right-brack)))

(install-procedure *command-formats* 'hole fmt:hole)

(define (cmd:fill expr1 expr2)
  (match expr1
    [(atom symbol)
     expr1]
    [(application applicator applicand)
     (application (cmd:fill applicator expr2)
                  (cmd:fill applicand expr2))]
    [(abstraction argument body)
     (abstraction argument (cmd:fill body expr2))]
    [(command name arguments)
     (if (eq? name 'hole)
         expr2
         expr1)]
    [_
     expr1]))

(install-procedure *commands* 'fill cmd:fill)

(define (fmt:fill expr1 expr2)
  (if (and (notation-abuse?)
           (or (abstraction? expr1) (application? expr1)))
      (format "~a~a~a~a~a~a"
              (fmt:left-paren)
              (format-expr expr1)
              (fmt:right-paren)
              (fmt:left-brack)
              (format-expr expr2)
              (fmt:right-brack))
      (format "~a~a~a~a"
              (format-expr expr1)
              (fmt:left-brack)
              (format-expr expr2)
              (fmt:right-brack))))

(install-procedure *command-formats* 'fill fmt:fill)
\end{lstlisting}

\section{Módulo interfaz}
\label{ap:2:interfaz}

\begin{lstlisting}[language=racket]
#lang racket/base

(require racket/cmdline)

(require "lambda-common.rkt")
(require "lambda-commands.rkt")

(define (repl)
  (parameterize ([current-prompt-read
                  (lambda ()
                    (display "lambda> ")
                    (flush-output)
                    (let ([str (read-line)])
                      (if (or (eof-object? str) (string=? str "exit"))
                          eof
                          (read-expr (open-input-string str)))))]
                 [current-eval
                  (lambda (expr)
                    (eval-expr (cdr expr)))]
                 [current-print
                  (lambda (expr)
                    (print-expr expr)
                    (newline))])
    (display "REPL ready, press any key to start...")
    (flush-output)
    (read-line)
    (read-eval-print-loop)))

(define console-args (command-line #:program "texlambda"))

(repl)
\end{lstlisting}

\section{Módulo \TeX}
\label{ap:2:tex}

\begin{lstlisting}[language=racket]
#lang racket/base

(require racket/system)
(require racket/file)
(require racket/cmdline)
(require racket/list)

(require "lambda-common.rkt")
(require "lambda-commands.rkt")

(fmt:left-paren "\\left( ")
(fmt:right-paren " \\right)")
(fmt:left-brack "\\left[ ")
(fmt:right-brack " \\right]")
(fmt:command-name-pre "\\texttt{")
(fmt:command-name-post "}")
(fmt:application-sep "\\ ")
(fmt:abstraction-sep "\\ ")
(fmt:command-sep "\\texttt{,}")
(fmt:lambda " \\lambda ")
(fmt:dot ".")
(fmt:fv-name "\\mathrm{FV}")
(fmt:length-pre "\\left\\|")
(fmt:length-post "\\right\\|")

(define (fmt:bold expr)
  (format "\\boldsymbol{~a}"
          (format-expr expr)))

(install-procedure *command-formats* 'bold fmt:bold)

(define (fmt:context expr)
  (if (and (notation-abuse?)
           (or (abstraction? expr) (application? expr)))
      (format "~a~a~a~a \\quad ~a"
              (fmt:left-paren)
              (format-expr expr)
              (fmt:right-paren)
              (fmt:left-brack)
              (fmt:right-brack))
      (format "~a~a \\quad ~a"
              (format-expr expr)
              (fmt:left-brack)
              (fmt:right-brack))))

(install-procedure *command-formats* 'context fmt:context)

(define (fmt:prime expr n)
  (format "~a^{~a}"
          (format-expr expr)
          (make-string n #\')))

(install-procedure *command-formats* 'prime fmt:prime)

(define (fmt:subscript expr sub)
  (format "~a_{~a}"
          (format-expr expr)
          (format-expr sub)))

(install-procedure *command-formats* 'subscript fmt:subscript)

(define (fmt:superscript expr sup)
  (format "~a^{~a}"
          (format-expr expr)
          (format-expr sup)))

(install-procedure *command-formats* 'superscript fmt:superscript)

(define (fmt:seq expr)
  (format "\\vec{~a}"
          (format-expr expr)))

(install-procedure *command-formats* 'seq fmt:seq)

(define (fmt:dots pos . exprs)
  (let ([head (take exprs pos)]
        [tail (drop exprs pos)])
    (format-list (append head (list "...") tail) ",")))

(install-procedure *command-formats* 'dots fmt:dots)

(define (fmt:subterm expr1 expr2)
  (format "~a \\subset ~a"
          (format-expr expr1)
          (format-expr expr2)))

(install-procedure *command-formats* 'subterm fmt:subterm)

(define (fmt:subterms expr)
  (format "\\mathrm{Sub}\\left( ~a \\right)"
          (format-expr expr)))

(install-procedure *command-formats* 'subterms fmt:subterms)

(define (fmt:left-apply sup expr1 expr2)
  (format-application
   (command 'superscript (list expr1 sup))
   expr2))

(install-procedure *command-formats* 'left-apply fmt:left-apply)

(define (fmt:right-apply sup expr1 expr2)
  (format-application
   expr1
   (command 'superscript (list expr2 (format "\\sim ~a" (format-expr sup))))))

(install-procedure *command-formats* 'right-apply fmt:right-apply)

(define cache-dir
  (make-parameter "./lambda-cache/"))

(define (file-extension name ext)
  (string-append name ext))

(define (cache-path filename)
  (string-append (cache-dir) filename))

(define (tex-before-curly in out)
  (let loop [(c   (peek-char in))
             (lis null)]
    (if (char=? c #\})
        (list->string (reverse lis))
        (begin
          (write-char (read-char in) out)
          (loop (peek-char in) (cons c lis))))))

(define (tex-process-file filename)
  (call-with-input-file filename
    (lambda (in)
      (call-with-output-file (cache-path filename)
        (lambda (out)
          (let loop ([braces? #f])
            (cond
              [(eof-object? (peek-char in)) #t]
              [(string=? "\\lc{" (peek-string 4 0 in))
               (read-string 4 in)
               (print-expr (eval-expr (read-expr in)) out)
               (loop #t)]
              [(string=? "\\lc*{" (peek-string 5 0 in))
               (read-string 5 in)
               (cmd:abuse)
               (print-expr (eval-expr (read-expr in)) out)
               (cmd:unabuse)
               (loop #t)]
              [(char=? (peek-char in) #\})
               (if braces? (read-char in) (write-char (read-char in) out))
               (loop #f)]
              [(string=? "\\input{" (peek-string 7 0 in))
               (write-string (read-string 7 in) out)
               (let ([ref (tex-before-curly in out)])
                 (unless (file-exists? (string-append "./lambda-cache/" ref ".tex"))
                   (tex-process-file (string-append ref ".tex"))))
               (loop braces?)]
              [(string=? "\\bibliography{" (peek-string 14 0 in))
               (write-string (read-string 14 in) out)
               (let ([bib (tex-before-curly in out)])
                 (unless (file-exists? (string-append "./lambda-cache/" bib ".bib"))
                   (copy-file (string-append bib ".bib")
                              (string-append "./lambda-cache/" bib ".bib"))))
               (loop braces?)]
              [else
               (write-char (read-char in) out)
               (loop braces?)])))))))

(define (build-tex-draft entry)
  (system (format "pdflatex -draftmode -interaction=batchmode ~a >/dev/null" entry)))

(define (build-tex-bib entry)
  (system (format "bibtex ~a >/dev/null" entry)))

(define (build-tex-complete entry)
  (system (format "pdflatex -interaction=batchmode -shell-escape ~a >/dev/null" entry)))

(define (build-tex-document entry)
  (current-directory (cache-dir))
  (build-tex-draft entry)
  (build-tex-bib entry)
  (build-tex-draft entry)
  (build-tex-complete entry))

(define (texlambda entry)
  (when (directory-exists? (cache-dir))
    (delete-directory/files (cache-dir)))
  (make-directory (cache-dir))
  (tex-process-file (file-extension entry ".tex"))
  (build-tex-document entry))

(define console-args
  (command-line #:program "texlambda" #:args (entry) entry))

(texlambda console-args)
\end{lstlisting}

%%% Local Variables:
%%% mode: latex
%%% TeX-master: "main"
%%% End:
